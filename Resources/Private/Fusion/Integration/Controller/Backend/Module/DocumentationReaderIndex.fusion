UpAssist.DocumentationReader.Backend.Module.DocumentationReaderController.index = Neos.Fusion:Component {
  endpoints = ${Configuration.setting('UpAssist.DocumentationReader.documentationEndpoints')}
  documentationUrl = ${Configuration.setting('UpAssist.DocumentationReader.documentationUrl')}
  renderer = afx`
    <iframe src={props.documentationUrl} width="100%" height="100%" @if={!props.endpoints && props.documentationUrl}></iframe>
    <Neos.Fusion:Fragment @if={props.endpoints}>
      <div class="tab-wrap">
        <Neos.Fusion:Loop items={props.endpoints} itemName="endpoint">
          <input type="radio" id={'tab' + iterator.index} name="tabs" class="tab" checked="checked" checked.@if={iterator.isFirst} />
          <label for={'tab' + iterator.index}>{endpoint.label} <a href={endpoint.url} target="_blank" title="Open in a new window"><i class="fas fa-external-link-alt"></i></a></label>
        </Neos.Fusion:Loop>
        <Neos.Fusion:Loop items={props.endpoints} itemName="endpoint">
          <div id={'tab-content' + iterator.index} class="tab__content">
            <iframe src={endpoint.url} width="100%" height="100%"></iframe>
          </div>
        </Neos.Fusion:Loop>
      </div>
    </Neos.Fusion:Fragment>
  `
}
